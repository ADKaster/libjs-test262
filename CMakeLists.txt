cmake_minimum_required(VERSION 3.16)
project(libjs-test262-superbuild LANGUAGES NONE)

include(ExternalProject)

if (NOT SERENITY_SOURCE_DIR)
    message(FATAL_ERROR "SERENITY_SOURCE_DIR not set.")
endif()

set(LAGOM_SOURCE_DIR ${SERENITY_SOURCE_DIR}/Meta/Lagom)
set(LAGOM_BINARY_DIR ${SERENITY_SOURCE_DIR}/Build/lagom)
set(LAGOM_INSTALL_DIR ${CMAKE_CURRENT_BINARY_DIR}/lagom)

set_property(DIRECTORY PROPERTY EP_PREFIX "${PROJECT_BINARY_DIR}/ep-prefix")

ExternalProject_Add(Lagom
    PREFIX lagom
    SOURCE_DIR ${LAGOM_SOURCE_DIR}
    BINARY_DIR ${LAGOM_BINARY_DIR}
    INSTALL_DIR ${LAGOM_INSTALL_DIR}
    CMAKE_ARGS "-DBUILD_LAGOM=ON"
    CMAKE_ARGS "-DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>"
    BUILD_COMMAND ${CMAKE_COMMAND} --build <BINARY_DIR> --config $<CONFIG>
    BUILD_ALWAYS ON
    STEP_TARGETS install
    EXCLUDE_FROM_ALL YES
)

ExternalProject_Add(libjs-test262
    PREFIX libjs-262-build
    SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/src
    CMAKE_ARGS "-DCMAKE_PREFIX_PATH=${LAGOM_INSTALL_DIR}/share"
    CMAKE_ARGS "-DCMAKE_INSTALL_PREFIX=${CMAKE_CURRENT_BINARY_DIR}"
    DEPENDS Lagom-install
    STEP_TARGETS install
)
